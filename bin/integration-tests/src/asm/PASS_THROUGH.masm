use miden::protocol::active_note
use miden::protocol::output_note

# ERRORS
# =================================================================================================

# PASS_THROUGH script expects exactly 10 note inputs
const ERR_PASS_THROUGH_WRONG_NUMBER_OF_INPUTS="wrong number of inputs"

#! PASS_THROUGH script:
#! Creates a note consumable by pass-through, containing the same asset.
#!
#! Inputs:  []
#! Outputs: []
#!
#! Note inputs are assumed to be as follows:
#! - ASSET
#! - RECIPIENT
#! - [note_type, tag]
begin
    # drop note arguments
    dropw

    # store note inputs into memory starting at address 0
    push.0 exec.active_note::get_inputs
    # => [num_inputs, inputs_ptr]

    # make sure the number of inputs is 10
    eq.10 assert.err=ERR_PASS_THROUGH_WRONG_NUMBER_OF_INPUTS
    # => [inputs_ptr]

    # load ASSET
    mem_loadw_be
    # => [ASSET]

    padw mem_loadw_be.4
    # => [RECIPIENT, ASSET]

    # load note_type and tag from memory
    mem_load.8 mem_load.9
    # => [tag, note_type, RECIPIENT, ASSET]

    # create a note using inputs
    # Inputs: [tag, note_type, RECIPIENT]
    exec.output_note::create
    # => [note_idx, ASSET]

    movdn.4
    # => [ASSET, note_idx]
    # add asset to the note
    exec.output_note::add_asset
    # => []

    # clean stack
    dropw dropw dropw dropw
    # => []
end
